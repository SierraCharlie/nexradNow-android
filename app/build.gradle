buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
    }
}
apply plugin: 'com.android.application'

repositories {
    jcenter()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        applicationId "com.nexradnow.android.app"
        minSdkVersion 11
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/INDEX.LIST'

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.0'
    compile 'com.android.support:multidex:1.0.0'
    // RoboGuice for dependency injection
    compile 'org.roboguice:roboguice:3.+'
    provided 'org.roboguice:roboblender:3.+'
    // For the optional Nullable annotation
    compile 'com.google.code.findbugs:jsr305:1.3.9'
    // EventBus for inter-component communications
    compile 'de.greenrobot:eventbus:2.4.0'
    // Location services etc
    // TODO: include ONLY required services instead of the whole thing
    compile 'com.google.android.gms:play-services:7.8.0'
    // FTP client
    compile 'commons-net:commons-net:3.3'
    // IOUtils
    compile 'commons-io:commons-io:2.4'
    // netCDF
    compile 'edu.ucar:netcdf4:4.5.5'
    // validation (email)
    compile ('commons-validator:commons-validator:1.4.1') {
        exclude group: 'commons-beanutils', module: 'commons-beanutils'
        exclude group: 'commons-collections', module: 'commons-collections'
    }
    compile 'commons-beanutils:commons-beanutils-core:1.8.3'
    compile 'org.atteo.classindex:classindex:3.4'
}

// Package the preprocessed Annotations (ClassIndex) into the APK
tasks.withType(com.android.build.gradle.tasks.PackageApplication) { pkgTask ->
    android.applicationVariants.all { variant ->
        def String variantName = variant.getName().capitalize()
        if (pkgTask.getName().indexOf(variantName) != -1) {
            pkgTask.setJavaResourceDir(new File(buildDir, "intermediates/classes/$variant.dirName"))
        }
    }
}
